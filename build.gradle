/**
 * CMD ADAPTER - Command Line Task Driver
 *
 *  Copyright 2016 by Chad Juliano
 *
 *  Licensed under GNU Lesser General Public License v3.0 only.
 *  Some rights reserved. See LICENSE.
 *
 * @license LGPL-3.0 <http://spdx.org/licenses/LGPL-3.0>
 */

plugins {
    id 'project-report'
    id 'ru.vyarus.pom' version '1.1.0'
    id 'ru.vyarus.github-info' version '1.1.0'
}

repositories {
    jcenter()
    mavenLocal()
}

// include tasks and configuration to bundle this project as a launch4j executable.
apply from: 'launch4j.gradle'

dependencies {

    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

    testCompile 'junit:junit:4.12'

    runtime group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.22'
}

// the below block determines the files that get copied into the distribution.
// We create a new distribution called 'top' because 'main' has a different organization.
// https://docs.gradle.org/current/userguide/distribution_plugin.html
distributions {
    top {
        baseName = project.name
        contents {
            from(projectDir) { include 'README.md' }
            into('lib') {
                from(configurations.runtime.files)
                from(jar.outputs.files)
            }
            into('lib/config') {
            	from(sourceSets.main.resources.files) { include 'logback.xml' }
            }
        }
    }
}

// Junit test plugin
// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html
test {
    testLogging {
        showStandardStreams true
    }
}

// from github-info plugin. This adds details to the pom.
// https://github.com/xvik/gradle-github-info-plugin
github {
    user 'chadj2'
    license 'LGPLv3'
}

//from pom plugin. Simplifies pom generation.
// https://github.com/xvik/gradle-pom-plugin
pom {
    developers {
        developer {
            id 'chadj2'
            name 'Chad Juliano'
            email 'chadj@dallaslife.net'
        }
    }
}

// from the maven-publish plugin
// https://docs.gradle.org/current/userguide/maven_plugin.html
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
