/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.3/userguide/tutorial_java_projects.html
 */

plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

repositories {
    jcenter()
}

apply from: 'launch4j.gradle'

dependencies {

    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

    testCompile 'junit:junit:4.12'

    runtime group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.22'
}


distributions {
    main {
        baseName = project.name
        contents {
            from(createLaunch4j.outputs) {  include '*.exe' }
            from(createStartScripts.outputs) {
            	include '*.sh'
            	fileMode 0755
            }
            from(projectDir) { include 'README.md' }
            into('lib') {
                from(jar.outputs.files)
                from(configurations.runtime.files)
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('url', 'http://placeholder')
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

test {
    testLogging {
        showStandardStreams true
    }
}
